dnl Process this file with autoconf to produce a configure script.

dnl Need autoconf 2.50 or later for many features
AC_PREREQ(2.50)
AC_INIT(dbtools, 0.1-cvs)dnl FIXME: bugreport addr as third argument
AM_INIT_AUTOMAKE(dbtools, 0.1-cvs)dnl remove args once we require am1.6
AC_CONFIG_SRCDIR(source/xdb-manage.cc)

AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX

dnl Check for xapian bits
AC_PROG_LIBTOOL
XO_PATH_XAPIAN(, AC_MSG_ERROR([Can't find Xapian library]))

dnl Check for libxml or libxml2.  We do this by looking for xml-config.
AC_PATH_PROG(XML_CONFIG_PATH, xml2-config)
if test -n "$XML_CONFIG_PATH" ; then
  HAVE_LIBXML2=yes
  AC_DEFINE(HAVE_LIBXML2,, [Define if libxml2 is available.])
else
  HAVE_LIBXML2=no
  AC_PATH_PROG(XML_CONFIG_PATH, xml-config)
fi
AM_CONDITIONAL(HAVE_LIBXML2, test yes = "$HAVE_LIBXML2")

if test -n "$XML_CONFIG_PATH" ; then
  AC_DEFINE(HAVE_LIBXML,, [Define if libxml or libxml2 is available.])
  AC_MSG_CHECKING([libxml flags])
  LIBXML_CFLAGS="`$XML_CONFIG_PATH --cflags`"
  LIBXML_LIBS="`$XML_CONFIG_PATH --libs`"
  AC_MSG_RESULT("$LIBXML_CFLAGS")
else
  AC_MSG_ERROR([Can't build dbtool without libxml])
fi
AC_SUBST(LIBXML_CFLAGS)
AC_SUBST(LIBXML_LIBS)

dnl ******************************
dnl * Set special compiler flags *
dnl ******************************

dnl extra flags for building with gcc
if test yes = "$GCC"; then
  AM_CFLAGS="$AM_CFLAGS -Wall -Wunused -Wno-long-long"
  AM_CXXFLAGS="$AM_CXXFLAGS -Wall -Wunused -Wno-long-long -fno-gnu-keywords"
fi

dnl **************************
dnl * Build the output files *
dnl **************************

AC_OUTPUT(
 Makefile
 source/Makefile
)
