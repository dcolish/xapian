dnl Process this file with autoconf to produce a configure script.
AC_INIT(include/database.h)
AM_INIT_AUTOMAKE(om, 0.0.1)
AM_CONFIG_HEADER(config.h)

# Turn off shared libraries for now as the make the build take longer
AC_DISABLE_SHARED
AM_PROG_LIBTOOL

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX

dnl Checks for gtk / glib / glade
AC_MSG_CHECKING(for libraries needed for gui frontend)
have_glade=yes
AM_PATH_GTK(1.2.0, , have_glade=no)
AM_PATH_GLIB(1.2.0, , have_glade=no)
AM_PATH_LIBGLADE(, have_glade=no)
if test "X$have_glade" = Xyes; then
 AC_MSG_RESULT(found)
 AC_SUBST(GUI_SUBDIR, gui)
else
 AC_MSG_RESULT(not found)
 AC_SUBST(GUI_SUBDIR, )
fi

dnl Checks for libraries.
dnl empty action if found - libtool will take care of things
AC_CHECK_LIB(db, db_open, :)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_CHECK_FUNCS(strerror)

dnl extra flags for building with gcc
if test "$GCC" = yes; then
  changequote(, ) dnl avoid problems with [] in sed expressions
  CFLAGS=`echo "$CFLAGS -pedantic -Wall -Wunused -Wno-long-long" | sed 's/-O[0-9]*/-O6/'`
  case "$CFLAGS" in
    *-O6*) ;;
    *) CFLAGS="$CFLAGS -O6"
  esac

  CXXFLAGS=`echo "$CXXFLAGS -pedantic -Wall -Wunused -Wno-long-long -fno-gnu-keywords" | sed 's/-O[0-9]*/-O6/'`
  case "$CXXFLAGS" in
    *-O6*) ;;
    *) CXXFLAGS="$CXXFLAGS -O6"
  esac
  changequote([, ])
fi

AC_OUTPUT(
 Makefile
 stemmer/Makefile
 matcher/Makefile
 indexer/Makefile
 tests/Makefile
 backends/Makefile
 backends/da/Makefile
 backends/sleepy/Makefile
 backends/multi/Makefile
 backends/textfile/Makefile
 include/Makefile
 gui/Makefile
 gui/glade/Makefile
 ferretfx/Makefile)
